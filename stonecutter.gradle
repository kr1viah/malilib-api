plugins {
    id("dev.kikugie.stonecutter")
    id("net.fabricmc.fabric-loom-remap") version "1.14-SNAPSHOT" apply false
    id("me.modmuss50.mod-publish-plugin") version "1.1.0"
}

stonecutter.active "1.21.5"

boolean releaseGithub() {
    return providers.environmentVariable("RELEASE_GITHUB").getOrElse("false").toBoolean()
}

boolean releaseModrinth() {
    return providers.environmentVariable("RELEASE_MODRINTH").getOrElse("false").toBoolean()
}

boolean releaseMaven() {
    return providers.environmentVariable("RELEASE_MAVEN").getOrElse("false").toBoolean()
}

//noinspection GroovyAssignabilityCheck
stonecutter.tasks {
    if (releaseModrinth()) {
        order("publishModrinth")
    }
    if (releaseGithub()) {
        order("publishGithub")
    }
//    order("publishCurseforge")
}

version = project.mod_version + "+" + sc.current.version

tasks.register("runClient active version") {
    group = 'activeVersion'
    def wantedTask = 'runClient'
    dependsOn(rootProject.findProject(sc.current.version).tasks.named(wantedTask))
}
tasks.register("build active version") {
    group = 'activeVersion'
    def wantedTask = 'build'
    dependsOn(rootProject.findProject(sc.current.version).tasks.named(wantedTask))
}

publishMods {
    modLoaders.add("fabric")
    changelog = file("changelog.md").text
    type = STABLE

    if (releaseModrinth()) {
        if (!providers.environmentVariable("MODRINTH_TOKEN").isPresent() || providers.environmentVariable("MODRINTH_TOKEN").get() == "") {
            throw new GradleException('Missing MODRINTH_TOKEN')
        }
    }
    if (releaseGithub()) {
        if (!providers.environmentVariable("GITHUB_TOKEN").isPresent() || providers.environmentVariable("GITHUB_TOKEN").get() == "") {
            throw new GradleException('Missing GITHUB_TOKEN')
        }

        github {
            accessToken = providers.environmentVariable("GITHUB_TOKEN")
            repository = providers.environmentVariable("GITHUB_REPO")
            commitish = "master"
            tagName = "${rootProject.mod_version}"
            displayName = "${rootProject.mod_version}"
            allowEmptyFiles = true
        }
    }
}