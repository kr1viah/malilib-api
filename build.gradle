plugins {
    id 'fabric-loom' /*version '1.13-SNAPSHOT'*/
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

version = project.mod_version + "+" + sc.current.version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }
    mavenCentral()
}

loom {
    runConfigs.all {
        ideConfigGenerated = false
        runDir "../../run"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "com.github.sakura-ryoko:malilib:${project.malilib_version}"

    include "org.reflections:reflections:${project.reflections_version}"
    implementation "org.reflections:reflections:${project.reflections_version}"
    implementation "com.google.auto.service:auto-service-annotations:${project.auto_service_version}"
    implementation "com.google.code.gson:gson:${project.gson_version}"
    annotationProcessor "com.google.auto.service:auto-service:${project.auto_service_version}"
}

processResources {
    // note: 25w17a is the 3rd snapshot for 1.21.6

    // 1.21:    1.21   (inclusive) - 1.21.1  (inclusive)
    // 1.21.5:  1.21.1 (exclusive) - 1.21.5  (inclusive)
    // 1.21.8:  25w17a (inclusive) - 1.21.8  (inclusive)
    // 1.21.10: 1.21.9 (inclusive) - 1.21.10 (inclusive)
    // 1.21.11: 1.21.11
    def versions = ""

    switch (sc.current.version) {
        case "1.21" ->    versions = ">=1.21 <=1.21.1"
        case "1.21.5" ->  versions = ">1.21.1 <=1.21.5"
        case "1.21.8" ->  versions = ">=1.21.6 <=1.21.8" // I cant specify >=25w17a??
        case "1.21.10" -> versions = ">=1.21.9 <=1.21.10"
        case "1.21.11" -> versions = "1.21.11"
    }

    inputs.property "versions", versions
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "versions": versions
    }
}

tasks.register("collectFile") {
    group = "build"
    mustRunAfter("build")

    doLast {
        copy {
            from file("build/libs/${project.archives_base_name}-${project.mod_version}+${project.minecraft_version}.jar")
            into rootProject.file("build/libs")
        }
        copy {
            from file("build/libs/${project.archives_base_name}-${project.mod_version}+${project.minecraft_version}-sources.jar")
            into rootProject.file("build/libs")
        }
    }
}

tasks.register("buildAndCollect") {
    group = "build"
    dependsOn(tasks.named("build"), tasks.named("collectFile"))
}

def targetJavaVersion = 21

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishMods {
    file = remapJar.archiveFile
    changelog = " "
    type = STABLE
    modLoaders.add("fabric")

    if (providers.environmentVariable("RELEASE_GITHUB").getOrElse("false").toBoolean()) {
        if (!providers.environmentVariable("GITHUB_TOKEN").isPresent() || providers.environmentVariable("GITHUB_TOKEN").get() == "") {
            throw new GradleException('Missing GITHUB_TOKEN')
        }

        github {
            accessToken = providers.environmentVariable("GITHUB_TOKEN")
            repository = providers.environmentVariable("GITHUB_REPO")
            commitish = "master"
            tagName = project.mod_version
            displayName = version
        }
    }

    if (providers.environmentVariable("RELEASE_MODRINTH").getOrElse("false").toBoolean()) {
        if (!providers.environmentVariable("RELEASE_MODRINTH").isPresent() || providers.environmentVariable("RELEASE_MODRINTH").get() == "") {
            throw new GradleException('Missing RELEASE_MODRINTH')
        }
        modrinth {
            accessToken = providers.environmentVariable("MODRINTH_TOKEN")
            projectId = "Qppvg0Y6"

            def currentV = sc.current.version
            if (currentV != "1.21.11") {
                minecraftVersionRange {
                    switch (currentV) {
                        case "1.21" -> {
                            start = "1.21"
                            end = "1.21.1"
                        }
                        case "1.21.5" -> {
                            start = "24w33a" // first 1.21.2 snapshot
                            end = "1.21.5"
                        }
                        case "1.21.8" -> {
                            start = "25w17a" // 3rd 1.21.6 snapshot
                            end = "1.21.8"
                        }
                        case "1.21.10" -> {
                            start = "1.21.9"
                            end = "1.21.10"
                        }
                    }

                    includeSnapshots = true
                }
            } else {
                minecraftVersions.add("1.21.11")
            }

            requires("malilib")
        }
    }

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.kr1v'
            artifactId = "${project.archives_base_name}-${sc.current.version}"
            version = project.mod_version

            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}