plugins {
    id 'fabric-loom' /*version '1.13-SNAPSHOT'*/
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

sourceSets {
    register("testmod") {
        compileClasspath += main.output + main.compileClasspath
        runtimeClasspath += main.output + main.runtimeClasspath
    }
}

version = project.mod_version + "+" + sc.current.version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = "masa"
        url = "https://masa.dy.fi/maven/"
    }
    maven {
        name = "sakura-ryoko"
        url = "https://masa.dy.fi/maven/sakura-ryoko"
    }
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }
    maven {
        name = "kr1v"
        url = "https://repo.repsy.io/kr1v/maven/"
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
//    mappings loom.layered() {
//        it.mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
//        it.mappings rootProject.file("mappings/mappings.tiny")
//    }
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "${project.malilib}"

    modCompileOnly "com.terraformersmc:modmenu:${project.modmenu_version}"
    implementation "com.google.code.gson:gson:${project.gson_version}"

    include(implementation("net.kr1v:malilib-api-processor:1.0.0"))
    testmodAnnotationProcessor "net.kr1v:malilib-api-processor:1.0.0"
}

def requiredJava =
        sc.current.parsed.matches('>=1.20.5') ? JavaVersion.VERSION_21 :
                sc.current.parsed.matches('>=1.18')   ? JavaVersion.VERSION_17 :
                        sc.current.parsed.matches('>=1.17')   ? JavaVersion.VERSION_16 :
                                JavaVersion.VERSION_1_8


processResources {
    def versions = switch (sc.current.version) {
        case "1.15.2" ->  ">=1.14 <=1.15.2"
        case "1.16.5" ->  ">=1.16 <=1.16.5"
        case "1.17.1" ->  ">=1.17 <=1.17.1"
        case "1.19.4" ->  ">=1.18 <=1.19.4"
        case "1.20.1" ->  ">=1.20 <=1.20.1"
        case "1.20.4" ->  ">=1.20.3 <=1.20.4"
        case "1.21" ->    ">=1.21 <=1.21.1"
        case "1.21.5" ->  ">=1.21.2 <=1.21.5"
        case "1.21.8" ->  ">=1.21.6 <=1.21.8"
        case "1.21.10" -> ">=1.21.9 <=1.21.10"
        default ->        sc.current.version
    }

    inputs.property "versions", versions
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "versions": versions
    }
    def mixinJava = "JAVA_${requiredJava.majorVersion}"
    filesMatching("*.mixins.json") {
        expand(java: mixinJava)
    }
}

tasks.register("collectFile") {
    group = "build"
    mustRunAfter("build")

    doLast {
        copy {
            from file("build/libs/${project.archives_base_name}-${project.mod_version}+${project.minecraft_version}.jar")
            into rootProject.file("build/libs/${project.mod_version}")
        }
        copy {
            from file("build/libs/${project.archives_base_name}-${project.mod_version}+${project.minecraft_version}-sources.jar")
            into rootProject.file("build/libs/${project.mod_version}")
        }
    }
}

tasks.register("buildAndCollect") {
    group = "build"
    dependsOn(tasks.named("build"), tasks.named("collectFile"))
}

def targetJavaVersion = requiredJava.majorVersion.toInteger()

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

loom {
    runConfigs.all {
        ideConfigGenerated = false
        runDir "../../run"
    }
    runs {
        register("testmodClient") {
            client()
            configName = "Testmod Client"
            source(sourceSets["testmod"])
        }
    }
    mods {
        register("malilib-api") {
            sourceSet(sourceSets.main)
        }
        register("malilib-api-test") {
            sourceSet("testmod")
        }
    }

    fabricModJsonPath = rootProject.file("src/main/resources/fabric.mod.json")
}

java {
    withSourcesJar()
    sourceCompatibility = requiredJava
    targetCompatibility = requiredJava
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishMods {
    file = remapJar.archiveFile
    modLoaders.add("fabric")

    def startV = null
    def endV = null
    def currentV = sc.current.version

    switch (currentV) {
        case "1.15.2" -> {
            startV = "1.14"
            endV = "1.15.2"
        }
        case "1.16.5" -> {
            startV = "1.16"
            endV = "1.16.5"
        }
        case "1.17.1" -> {
            startV = "1.17"
            endV = "1.17.1"
        }
        case "1.19.4" -> {
            startV = "1.18"
            endV = "1.19.4"
        }
        case "1.20.1" -> {
            startV = "1.20"
            endV = "1.20.1"
        }
        case "1.20.4" -> {
            startV = "1.20.3"
            endV = "1.20.4"
        }
//                case "1.20.6" -> {
//                    startV = "1.20.5"
//                    endV = "1.20.6"
//                }
        case "1.21" -> {
            startV = "1.21"
            endV = "1.21.1"
        }
        case "1.21.5" -> {
            startV = "24w33a" // first 1.21.2 snapshot
            endV = "1.21.5"
        }
        case "1.21.8" -> {
            startV = "25w17a" // 3rd 1.21.6 snapshot
            endV = "1.21.8"
        }
        case "1.21.10" -> {
            startV = "1.21.9"
            endV = "1.21.10"
        }
        default -> {
            startV = currentV
        }
    }

    if (releaseGithub()) {
        github {
            accessToken = providers.environmentVariable("GITHUB_TOKEN")

            parent project(":").tasks.named("publishGithub")
        }
    }

    if (releaseCurseForge()) {
        curseforge {
            accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
            projectId = "1471872"

            if (endV != null) {
                minecraftVersionRange {
                    start = startV
                    end = endV

                    includeSnapshots = true
                }
            } else {
                minecraftVersions.add(startV)
            }

            javaVersions.add(requiredJava)

            clientRequired = true
            serverRequired = false

            requires("malilib")

            changelog = rootProject.file("changelog.md").text
            changelogType = "markdown"
        }
    }

    if (releaseModrinth()) {
        modrinth {
            accessToken = providers.environmentVariable("MODRINTH_TOKEN")
            projectId = "Qppvg0Y6"
            changelog = rootProject.file("changelog.md").text
            type = STABLE

            if (endV != null) {
                minecraftVersionRange {
                    start = startV
                    end = endV

                    includeSnapshots = true
                }
            } else {
                minecraftVersions.add(startV)
            }

            requires("malilib")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'net.kr1v'
            artifactId = "${project.archives_base_name}"
            version = project.mod_version + "-" + project.minecraft_version

            from components.java
        }
    }
    repositories {
        mavenLocal()
        if (releaseMaven()) {
            if (!providers.environmentVariable("REPSY_TOKEN").isPresent() || providers.environmentVariable("REPSY_TOKEN").get() == "") {
                throw new GradleException('Missing REPSY_TOKEN')
            }
            if (!providers.environmentVariable("REPSY_USERNAME").isPresent() || providers.environmentVariable("REPSY_USERNAME").get() == "") {
                throw new GradleException('Missing REPSY_USERNAME')
            }
            maven {
                name = "repsy"
                url = uri("https://repo.repsy.io/kr1v/maven/")
                credentials {
                    username = providers.environmentVariable("REPSY_USERNAME").get()
                    password = providers.environmentVariable("REPSY_TOKEN").get()
                }
            }
        }
    }
}

def runAll = false

tasks.named("runTestmodClient").configure {
//    if (!runAll) {
//        onlyIf {
//            def current = stonecutter.current.version
//            def active = stonecutter.active.version
//            return current == active
//        }
//    }
}

tasks.register("runAllTestClient") {
    runAll = true
    dependsOn("runTestmodClient")
}