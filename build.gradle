plugins {
    id 'fabric-loom' version '1.13-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}


repositories {
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }
    mavenCentral()
}

sourceSets {
    ap {
        java { srcDir "src/ap/java" }
        resources { srcDir "src/ap/resources" }
    }
}

configurations {
    apImplementation
    apAnnotationProcessor
    apImplementation.extendsFrom implementation

    apCompileClasspath {
        canBeResolved = true
        extendsFrom apImplementation
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation sourceSets.ap.output
    modImplementation "com.github.sakura-ryoko:malilib:${project.malilib_version}"
    implementation sourceSets.ap.output

    apImplementation "com.google.auto.service:auto-service-annotations:1.1.1"
    apImplementation "com.google.code.gson:gson:2.13.2"
    apAnnotationProcessor "com.google.auto.service:auto-service:1.1.1"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
def apGeneratedDir = layout.buildDirectory.dir("generated/sources/annotations/main")

afterEvaluate {
    if (configurations.named("apCompileClasspath") && configurations.named("apAnnotationProcessor")) {
        configurations.apCompileClasspath.extendsFrom(configurations.apAnnotationProcessor)
    }
}

tasks.named('compileApJava', JavaCompile).configure { JavaCompile jc ->
    jc.group = "build"
    jc.description = "Compile annotation processor"
    jc.source = sourceSets.ap.java
    jc.classpath = files(sourceSets.ap.compileClasspath, configurations.apCompileClasspath)
    jc.destinationDirectory.set(layout.buildDirectory.dir("classes/java/ap"))
    jc.options.encoding = "UTF-8"
    jc.options.annotationProcessorPath = configurations.apAnnotationProcessor
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        jc.options.release.set(targetJavaVersion)
    } else {
        jc.options.compilerArgs += ["--release", "${targetJavaVersion}"]
    }
}

sourceSets.main.java.srcDir apGeneratedDir

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
    if (it.name == 'compileApJava') return
    dependsOn tasks.named('compileApJava')
    it.options.annotationProcessorPath = files(layout.buildDirectory.dir("classes/java/ap"), configurations.apCompileClasspath)
    it.options.generatedSourceOutputDirectory.set(apGeneratedDir)
    it.doFirst { apGeneratedDir.get().asFile.mkdirs() }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.kr1v'
            artifactId = 'malilib-api'
            version = project.version

            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}