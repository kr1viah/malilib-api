plugins {
    id 'fabric-loom' /*version '1.13-SNAPSHOT'*/
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

sourceSets {
    create("testmod") {
        compileClasspath += main.output + main.compileClasspath
        runtimeClasspath += main.output + main.runtimeClasspath
    }
}

version = project.mod_version + "+" + sc.current.version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "com.github.sakura-ryoko:malilib:${project.malilib_version}"

    implementation "com.google.auto.service:auto-service-annotations:${project.auto_service_version}"
    implementation "com.google.code.gson:gson:${project.gson_version}"
    annotationProcessor "com.google.auto.service:auto-service:${project.auto_service_version}"

    // cursed
    testmodAnnotationProcessor "net.kr1v:malilib-api:0.2.2-1.21.5"
}

processResources {
    // note: 25w17a is the 3rd snapshot for 1.21.6

    // 1.21:    1.21   (inclusive) - 1.21.1  (inclusive)
    // 1.21.5:  1.21.1 (exclusive) - 1.21.5  (inclusive)
    // 1.21.8:  25w17a (inclusive) - 1.21.8  (inclusive)
    // 1.21.10: 1.21.9 (inclusive) - 1.21.10 (inclusive)
    // 1.21.11: 1.21.11
    def versions = ""

    switch (sc.current.version) {
        case "1.21" ->    versions = ">=1.21 <=1.21.1"
        case "1.21.5" ->  versions = ">1.21.1 <=1.21.5"
        case "1.21.8" ->  versions = ">=1.21.6 <=1.21.8" // I cant specify >=25w17a??
        case "1.21.10" -> versions = ">=1.21.9 <=1.21.10"
        case "1.21.11" -> versions = "1.21.11"
    }

    inputs.property "versions", versions
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "versions": versions
    }
}

tasks.register("collectFile") {
    group = "build"
    mustRunAfter("build")

    doLast {
        copy {
            from file("build/libs/${project.archives_base_name}-${project.mod_version}+${project.minecraft_version}.jar")
            into rootProject.file("build/libs")
        }
        copy {
            from file("build/libs/${project.archives_base_name}-${project.mod_version}+${project.minecraft_version}-sources.jar")
            into rootProject.file("build/libs")
        }
    }
}

tasks.register("buildAndCollect") {
    group = "build"
    dependsOn(tasks.named("build"), tasks.named("collectFile"))
}

def targetJavaVersion = 21

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

loom {
    runConfigs.all {
        ideConfigGenerated = false
        runDir "../../run"
    }
    runs {
        create("testmodClient") {
            client()
            configName = "Testmod Client"
            source(sourceSets["testmod"])
        }
    }
    mods {
        create("malilib-api") {
            sourceSet(sourceSets.main)
        }
        create("malilib-api-test") {
            sourceSet("testmod")
        }
    }

    fabricModJsonPath = rootProject.file("src/main/resources/fabric.mod.json")
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishMods {
    file = remapJar.archiveFile
    changelog = " "
    type = STABLE
    modLoaders.add("fabric")

    if (providers.environmentVariable("RELEASE_GITHUB").getOrElse("false").toBoolean()) {
        github {
            accessToken = providers.environmentVariable("GITHUB_TOKEN")

            parent project(":").tasks.named("publishGithub")
        }
    }

    if (providers.environmentVariable("RELEASE_MODRINTH").getOrElse("false").toBoolean()) {
        modrinth {
            accessToken = providers.environmentVariable("MODRINTH_TOKEN")
            projectId = "Qppvg0Y6"

            def currentV = sc.current.version
            if (currentV != "1.21.11") {
                minecraftVersionRange {
                    switch (currentV) {
                        case "1.21" -> {
                            start = "1.21"
                            end = "1.21.1"
                        }
                        case "1.21.5" -> {
                            start = "24w33a" // first 1.21.2 snapshot
                            end = "1.21.5"
                        }
                        case "1.21.8" -> {
                            start = "25w17a" // 3rd 1.21.6 snapshot
                            end = "1.21.8"
                        }
                        case "1.21.10" -> {
                            start = "1.21.9"
                            end = "1.21.10"
                        }
                    }

                    includeSnapshots = true
                }
            } else {
                minecraftVersions.add("1.21.11")
            }

            requires("malilib")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'net.kr1v'
            artifactId = "${project.archives_base_name}"
            version = project.mod_version + "-" + project.minecraft_version

            from components.java
        }
    }
    repositories {
        mavenLocal()
        if (providers.environmentVariable("RELEASE_MAVEN").getOrElse("false").toBoolean()) {
            if (!providers.environmentVariable("REPSY_TOKEN").isPresent() || providers.environmentVariable("REPSY_TOKEN").get() == "") {
                throw new GradleException('Missing REPSY_TOKEN')
            }
            if (!providers.environmentVariable("REPSY_USERNAME").isPresent() || providers.environmentVariable("REPSY_USERNAME").get() == "") {
                throw new GradleException('Missing REPSY_USERNAME')
            }
            maven {
                name = "repsy"
                url = uri("https://repo.repsy.io/kr1v/maven/")
                credentials {
                    username = providers.environmentVariable("REPSY_USERNAME").get()
                    password = providers.environmentVariable("REPSY_TOKEN").get()
                }
            }
        }
    }
}